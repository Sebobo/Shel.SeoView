prototype(Shel.SeoView:ContentStatistics) < prototype(Neos.Fusion:Tag) {
    tagName = 'section'
    attributes.class = 'seo-view__text-analysis'

    @context.pageText = Neos.Fusion:Collection {
        collection = ${q(documentNode).children('[instanceof Neos.Neos:ContentCollection]').find('[instanceof Neos.Neos:Content]').get()}

        itemRenderer = Neos.Fusion:Value {
            value = ${q(item).property('title') + ' ' + q(item).property('text') + ' '}
        }
    }

    content = Neos.Fusion:Array {
        title = Neos.Fusion:Tag {
            tagName = 'h4'
            content = ${I18n.translate('Shel.SeoView:Main:sections.contentStatistics.title')}
        }

        statistics = Neos.Fusion:Tag {
            tagName = 'table'
            content = Neos.Fusion:Array {
                characterCount = Neos.Fusion:Tag {
                    tagName = 'tr'
                    content = Neos.Fusion:Array {
                        key = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${I18n.translate('Shel.SeoView:Main:sections.contentStatistics.characterCount')}
                        }
                        value = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${Readability.characterCount(pageText)}
                        }
                    }
                }

                wordCount = Neos.Fusion:Tag {
                    tagName = 'tr'
                    content = Neos.Fusion:Array {
                        key = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${I18n.translate('Shel.SeoView:Main:sections.contentStatistics.wordCount')}
                        }
                        value = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${Readability.wordCount(pageText)}
                            content.@process.showGood = ${value >= 200 ? '<span class="occurence-good">' + value + '</span>' : value}
                        }
                    }
                }

                sentenceCount = Neos.Fusion:Tag {
                    tagName = 'tr'
                    content = Neos.Fusion:Array {
                        key = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${I18n.translate('Shel.SeoView:Main:sections.contentStatistics.sentenceCount')}
                        }
                        value = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${Readability.sentenceCount(pageText)}
                        }
                    }
                }

                averageWordsPerSentence = Neos.Fusion:Tag {
                    tagName = 'tr'
                    content = Neos.Fusion:Array {
                        key = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${I18n.translate('Shel.SeoView:Main:sections.contentStatistics.averageWordsPerSentence')}
                        }
                        value = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${Math.round(Readability.averageWordsPerSentence(pageText), 2)}
                        }
                    }
                }

                readingDuration = Neos.Fusion:Tag {
                    tagName = 'tr'
                    content = Neos.Fusion:Array {
                        key = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${I18n.translate('Shel.SeoView:Main:sections.contentStatistics.readingDuration')}
                        }
                        value = Neos.Fusion:Tag {
                            tagName = 'td'
                            content = ${Math.round((Readability.wordCount(pageText) / 220))}
                            content.@process.showLow = ${value > 5 || value <= 1 ? '<span class="occurence-low">' + value + '</span>' : value}
                            content.@process.showGood = ${value <= 5 && value > 1 ? '<span class="occurence-good">' + value + '</span>' : value}
                        }
                    }
                }
            }
        }
    }
}
